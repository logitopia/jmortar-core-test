/*
 *     JMortar - Tools to make your Java life easier.
 *     Copyright (C) 2015-2017 Stephen Cheesley
 *
 *     This program is free software: you can redistribute it and/or modify
 *     it under the terms of the GNU General Public License as published by
 *     the Free Software Foundation, either version 3 of the License, or
 *     (at your option) any later version.
 *
 *     This program is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU General Public License for more details.
 *
 *     You should have received a copy of the GNU General Public License
 *     along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
import groovy.json.JsonSlurper
import groovy.json.JsonBuilder

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0"
    }
}

apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'com.logitopia'
version = '1.0.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

bintray {
    user = bintrayUser
    key = bintrayKey
    pkg {
        repo = 'jmortar-release'
        name = 'jmortar-core-test'
        userOrg = 'logitopia'
        publications = ['publishToBintray']
    }
    publish = true
}

repositories {
    maven { url "http://repo.maven.apache.org/maven2" }

    mavenLocal()
    jcenter()
}

publishing {
    publications {
        publishToBintray(MavenPublication) {
            from components.java
            groupId 'com.logitopia'
            artifactId 'jmortar-core-test'
            version version
//            vcsTag = '1.3.0'
        }
    }
    repositories {
        mavenLocal()
    }
}

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.25'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task buildJavadoc( type : Javadoc) {
    group 'Create a release'
    description 'Builds all required Javadoc and outputs it to the sites directory that it is published.'

    // Make a directory for this release
    def outputDir = "docs/javadoc/${version.replaceAll("\\."  ,  "_")}"
    File versionDir = new File(outputDir)
    versionDir.mkdirs()

    // Build javadoc for all of the Java source (non-optional)
    source = sourceSets.main.allJava
    classpath = configurations.compile

    destinationDir = versionDir
}

task testReports( type : Test ) {
    group 'Create a release'
    description 'Runs test reports and outputs them to the sites directory so that they are published.'

    // Store the test reports into our documentation site
    reports.junitXml.destination = new File("docs/TestReport/xml")
    reports.html.destination = new File("docs/TestReport")
}

task writeReleaseVersion() {
    group 'Create a release'
    description 'Write the latest release number to the supported_releases file'

    def releaseFile = new File("docs/supported_releases.json")
    def releases = new JsonSlurper().parseText(releaseFile.text)

    if (!releases.contains(version)) {
        releases.add(version)
    }

    releaseFile.write(new JsonBuilder(releases.toSorted { a, b -> b <=> a }).toPrettyString())
}

task prepareRelease() {
    dependsOn 'testReports'
    dependsOn 'buildJavadoc'
    dependsOn 'writeReleaseVersion'

    group 'Create a release'
    description 'Creates a release by generating the necessary developer documentation'
}
